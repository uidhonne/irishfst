
!***********************************************************************
! NUMERALS
! Cardinal numbers are described seperately here rather than with other
! Adjectives
! For Personal Numerals (duine, beirt, triúr) SEE NOUNS
!***********************************************************************
Multichar_Symbols

+Num		! Numeral
+Card		! Cardinal (one two three ...)
+Ord		! Ordinal (first, second, third..) i.e.  mo dhá lámh, an chéad dhá theach 
+Def		! after definite article etc. (an, na, aon, céad)
+Len
+Ecl
+hPref
+Op
^IM
^Urú
^Sé
^hv
^tv
^ts

LEXICON Root
	Numerals;

LEXICON Numerals

!!!! CARDINAL !!!!
aon+Num+Card:aon			NumSéUrú;
aon+Num+Card:haon			#;
aon+Num+Card+Def:t-aon		#;
dó+Num+Card:dó				NumSéUrú; ! a dó (two)
dó+Num+Card:dhá				#; ! with a noun: dhá chapall (two horses)
dó+Num+Card+Def:dá			#;
trí+Num+Card:trí			NumSéUrú;
ceathair+Num+Card:ceathair	NumSéUrú; ! a ceathair (four)
ceathair+Num+Card:ceithre	NumSéUrú; ! ceithre chapall (four horses)
cúig+Num+Card:cúig			NumSéUrú;
sé+Num+Card:sé				NumSéUrú;
seacht+Num+Card:seacht		NumSéUrú;
ocht+Num+Card:ocht			NumSéUrú;
naoi+Num+Card:naoi			NumSéUrú;
deich+Num+Card:deich		NumSéUrú;
fiche+Num+Card:fiche		NumSéUrú;
tríocha+Num+Card:tríocha	NumSéUrú;
daichead+Num+Card:daichead	NumSéUrú;
caoga+Num+Card:caoga		NumSéUrú;
seasca+Num+Card:seasca		NumSéUrú;
seachtó+Num+Card:seachtó	NumSéUrú;
ochtó+Num+Card:ochtó		NumSéUrú;
ochtó+Num+Card:t-ochtó		#;
nócha+Num+Card:nócha		NumSéUrú;
céad+Num+Card:céad			NumSéUrú; ! hundred
míle+Num+Card:míle			NumSéUrú;
milliún+Num+Card:milliún	NumSéUrú;

!!!! ORDINAL !!!!

céad+Num+Ord:céad			NumSéUrú; ! first
!!céad+Num+Ord+Def:chéad	#;
aon+Num+Ord:aonú			NumSéUrú;
aon+Num+Ord+Def:t-aonú		#;
dara+Num+Ord:dara			NumSéUrú;
dó+Num+Ord:dóú				NumSéUrú;
treas+Num+Ord:treas			NumSéUrú;
trí+Num+Ord:tríú			NumSéUrú;
ceathair+Num+Ord:ceathrú	NumSéUrú;
cúig+Num+Ord:cúigiú			NumSéUrú;
sé+Num+Ord:séú				NumSéUrú;
seacht+Num+Ord:seachtú		NumSéUrú;
ocht+Num+Ord:ochtú			NumSéUrú;
!!ocht+Num+Ord+Def:t-ochtú	#;
naoi+Num+Ord:naoú			NumSéUrú;
deich+Num+Ord:deichiú		NumSéUrú;

!!!! Operators !!!!
++Num+Op:+	#;
-+Num+Op:-	#;
*+Num+Op:*	#;
=+Num+Op:=	#;
/+Num+Op:/	#;
÷+Num+Op:÷	#;

LEXICON NumSéUrú
#;
+Len:^IM^Sé^tv		#;
+Ecl:^IM^Urú		#;
+hPref:^IM^hv^ts		#;
