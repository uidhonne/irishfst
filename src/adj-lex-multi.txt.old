!****************************************************************************
! change:
! July 2004: 
! removed +Len +Ecl from lexical level see  LEXICON SéUrú
! removed +hPref see LEXICON Base
! Nov 04
! relaced +Len +Ecl in lexical level see  LEXICON SéUrú
! added Its as in go_leor+Adj+Its
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Na hAidiactaí Tuairisciúla - Descriptive Adjectives
!
!  E. Uí Dhonnchadha
! (c)2001
! (1) New Irish Grammar by The Christian Brothers  
! (2) Réchúrsa Gramadaí, Brian Mac Giolla Phádraig
! (3) Graméir Gaeilge na mBráithre Críostaí
! (4) Nuachúrsa Gaeilge na mBráithre Críostaí
! (5) A Handbook of Irish Vol 2.An Outline of Modern Irish Grammar,
!     Alfred Bammesberger
! (6) Ó Dónaill Irish-English Dictionary
! (7) Gléacht - Ó Dónaill Online Dictionary 
!
! Descriptive adjectives can be used in two ways (REF P59 NIG)
!
! a) predicatively - quailifies a noun/pronoun *indirectly* as a predicate 
!    or part of a predicate
!    E.G. Is BREÁ an lá é (It is a LOVELY day, lit. it is lovely that the day is (approx!)
!    E.G. Tá mé TUIRSEACH (I am TIRED)
!    E.G. Tá sé FUAR (It is COLD)
!    Used in this way the base form IS NOT INFLECTED to agree with case/number/gender
!    of the noun - except in a few cases ... see p 59 s4 NIG
!    The base form (positive degree) is used
!
! b) attributively - qualifies a noun *directly*
!    E.G. Tá an páiste ÓG ar scoil (The YOUNG child is a at school
!    E.G. Tá an fhuinneog GHLAN (The window is CLEAN)
!    E.G. Níl pingin RUA agam (I haven't a (RED) penny)
!
!    Used in this form it IS INFLECTED to agree with case/number/gender of the noun
!
!    The adjective usually follows the noun though some forms can be prefixed
!    to the noun e.g. SEANfhear (old man) LAGmhisneach?? ((in) low spirits),
!    ARDeagalis (cathedral - lit. high church)
! 
!****************************************************************************

Multichar_Symbols


+Adj	! adjective

+Base	! positive / base form (changed from +Pos to +Base 10/09/03)
+Comp	! comparative
! +Sup	! superlative

+Masc	! masculine gender
+Fem	! feminine gender

+Com	! nominative case
+Gen	! genitive case 
+Voc	! vocative case 

+Sg	! singular
+Pl	! plural
!+Sé	! e.g. ab fhearr, ba mhó
!+Urú	! e.g. i ngach

+Weak	! when an adj is qualifying a strong plural noun(i.e. noun plural is the 
+Strong	! same for all cases) the adj will also have the same form in all cases
	! BUT when the adj. is qualifying  a weak plural noun,in the gen.case, it  
	! is not inflected i.e. the base form of the adjective is used. 
	! Nom and Voc plurals are inflecte as for strong plural nouns

+Slender ! qualifies a plural noun ending in a slender consonant
+NotSlen ! qualifies a plural noun ending in a broad consonantor a vowel

!+LenNO	! after prepositions (e.g. ag an (at the), 
	! ar an (on the), as an (out of the) etc)
!+LenYES 
+hPref	! prefix e.g. (h)iontach
+Ecl	! eclipsis - urú; e.g. i ngach
+Len	! e.g. ab fhearr, ba mhó
	! a masc  noun must be either lenitedor eclipsed according to 
	! preference/dialect. If it is lenited then the adj is likewise lenited.
	! If it is eclipsed then the adj has no initial mutation.

	! After simple prepositions (e.g. den (from it), don (to it), sa (in it),
	! san (in it - after vowel initial noun)) both masc and fem
	! nouns are lenited where possible, an adj following a masc noun which is 
	! lenited may be *optionally* lenited also. (dialect based ???)
+Cop	 ! with Copail "is" e.g. b'fhearr
+Its	! Adj+Its

^Adj	! Adjective- used in initial mutations
^Sé	! Séimhiú (softening) Lenition - h added after certain initial 
	! consonants (bcdfgmpst)
^Urú	! eclipsis e.g. i ngach 
^Ath	! Athrú (change) word ending
^Caol	! Caolú (slenderise)- Attenuation : ie slenderise the end of word 
	! (see P9 NIG). 
	! Usually by adding an "i" after  the last broad vowel 
	! aouáóú are broad vowels; ieíé are slender vowels  
	! eg "an cat" (the cat) in nom. pl. becomes "na cait" (the cats) 
^Lea	! Broaden
^Coim	! Syncopate
^IM	! initial mutation
^hv	! h before vowel
!**************************************************************************  
!
!					L E X I C O N
!
!**************************************************************************  
LEXICON Root
	AdjA;				! ORIGINAL TEST LEXICON
	AdjIrregular;			! ORIGINAL TEST LEXICON
	Adj-BaseOnly;	!AdjBASE;	! ORIGINAL TEST LEXICON
	Adj-IrregComp;	!AI-COMP;	! ORIGINAL TEST LEXICON
	AdjB;				! punk adjs
	AdjC;				! FP adjs - auto
	AdjDath; 			! colours
	AdjE;				! FP adjs - manual
	Adj-FGB1;			! Foclóir Gaeilge Béarla Uí Dhónaill
	Adj-FGB2;			! Foclóir Gaeilge Béarla Uí Dhónaill
	AdjVariants;			! Adj Variants in FGB
	AdjEqualVariants;		! Adj Variants with Equal Sign in FGB
!************************************************************************** 
!	___________________________________________________
!	Category			*Genitive Singular			Pls
!					Masc		Fem	
!	___________________________________________________
!	1.1				D 4			D 2				+a
!	1.2				D 1			D 2				+a
!	1.3				D 1?		D 2?			+a
!	1.4				D 4			D ?2			+a
!	1.5				D 4			D ?2			+a
!	2.1				D 4			D 3				+a
!	3.1				D 4			D 4				--
!	4.1				D 4			D 3				(+a/e)
!
!	* The D refers to noun declensions
!************************************************************************** 
!					T Y P E   1
!					-----------
!			All end in a broad consonant
!**************************************************************************  
! 1st. Declension TYPE 1
!
!   	MONOSYLLABIC & ENDING IN	-LL (except dall)
!							-NN (except donn)
!							-CH(T) (except bocht)
!			   				& SOME OTHERS
!
!	Genitive Singular Masc.	} no change (like Noun Declension 4)
!	Vocative Singular Masc.	}
!
!	Genitive Singular Fem.	: attenuate & add "e" (like Noun Declension 2)
!
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "a"
!
!************************************************************************** 
!**************************************************************************  
!
! 1st. Declension TYPE 2
!
!   	MONOSYLLABIC : OTHERS
!	AND
! 	POLYSYLLABIC & ENDING IN -(MH)AR
!
!
!     **Genitive Singular Masc.	} attenuate	(Noun Declension 1)
!	Vocative Singular Masc.	}

!	Genitive Singular Fem.	: attenuate & add "e" ( Noun Declension 2)
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "a"
!
!************************************************************************** 

!---------------------------------------------------------------------------
! 1st. Declension TYPE 3
!
! 	POLYSYLLABIC & ENDING IN 	-ACH
!							-EACH
!
!     **Genitive Singular Masc.	} change each->igh, ach->aigh (like Noun Declension 1)
!     **Vocative Singular Masc.	}
!
!     **Genitive Singular Fem.	: change each->í,   ach->aí   (like Noun Declension 2)
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "a"
!
!************************************************************************** 
!
!**************************************************************************  
! 1st. Declension TYPE 4
!
! 	POLYSYLLABIC & ENDING IN 	-ÍOCH
!
!	Genitive Singular Masc.	} no change         (like Noun Declension 4)
!	Vocative Singular Masc.	}

!	Genitive Singular Fem.	: och -> [] + (thí) (like Noun Declension ???????)
!                                                    OR IS IT och -> [] + che ???????
!													like comparative
!	I HAVE DECIDED TO USE "-THÍ" - SEE BUÍOCH BELOW
!	AS IT IS IN CAIGHDEÁIN OIFIGIÚIL (AND ALSO IT WILL BE EASIER TO CHANGE 
!       BACK TO USING SAME AS COMPARATIVE FORM IF NECESSARY AT A LATER STAGE).
!
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "a"
!
!************************************************************************** 
!**************************************************************************  
! 1st. Declension TYPE 5
!
! 	POLYSYLLABIC & ENDING IN 	LONG VOWEL + CH (except lách)
!
!	Genitive Singular Masc.	} no change		(like Noun Declension 4)
!	Vocative Singular Masc.	} same as Gen Sg.
!
!	Genitive Singular Fem.	: ch -> [] + thaí OR IS IT ch -> [] + iche ???????
!													like comparative
!
!	I HAVE DECIDED TO USE -THAÍ FROM CAIGHDEÁN OIFIGIÚIL - SEE GNÁCH BELOW
!
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "a"
!
!************************************************************************** 
!
!************************************************************************** 
!					T Y P E   2
!					-----------
!			All end in a slender consonant
!**************************************************************************  
! 2nd. Declension TYPE 1
!
! 	POLYSYLLABIC & ENDS IN 	-IÚIL
!						-(A)IR
!
!	Genitive Singular Masc.	} no change		(like Noun Declension 4)
!	Vocative Singular Masc.	}

!	Genitive Singular Fem.	: broaden & add "a"(like Noun Declension 3)
!
!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: broaden & add "a"
!
!************************************************************************** 
!


!**************************************************************************  
! 2nd. Declension TYPE 2
!
! 	OTHERS
!
!	Genitive Singular Masc.	} no change		(like Noun Declension 4)
!	Vocative Singular Masc.	} 
!
!	Genitive Singular Fem.	: add "e"			(like Noun Declension 2 
!											 except already slender)
!

!	Vocative Singular Fem.	: same as Nom Sg.
!
!	Common Plural			: add "e"
!
!************************************************************************** 
!************************************************************************** 
!					T Y P E   3
!					-----------
!			      All end in a vowel
!**************************************************************************  
! 3rd. Declension TYPE 1
!
! 	ALL (except a few cases - i.e. breá, te)
!
!	Singular : No Inflection

!************************************************************************** 
!					T Y P E   4
!					-----------
!		All syncopated in the plural
!
!		Mostly syncopated in Fem Gen Sg.
!		Slenderised in Masc Gen Sg,
!
!**************************************************************************  
!
!**************************************************************************  
!
!		C O N T I N U A T I O N     C L A S S E S
!
!**************************************************************************  
LEXICON Adj1-1
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2A_sg;		!!!GEN/COMPARATIVE
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-A;
+Adj+Com+Slender:^Adj	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-A;
+Adj+Voc:^Adj			PL-A;


LEXICON Adj1-2
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2A_sg;
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-A;
+Adj+Com+Slender:^Adj	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-A;
+Adj+Voc:^Adj			PL-A;


LEXICON Adj1-3
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2B_sg;
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-A;
+Adj+Com+Slender:^Adj	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-A;
+Adj+Voc:^Adj			PL-A;


LEXICON Adj1-4
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2C_sg;	
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-A;
+Adj+Com+Slender:^Adj	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-A;
+Adj+Voc:^Adj			PL-A;


LEXICON Adj1-5
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2D_sg;	
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-A;
+Adj+Com+Slender:^Adj	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-A;
+Adj+Voc:^Adj			PL-A;


LEXICON Adj2-1
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D3_sg;	
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

+Adj+Com+NotSlen:^Adj^Lea	PL-A;
+Adj+Com+Slender:^Adj^Lea	PL-A-SLENDER;
+Adj+Gen+Weak:^Adj			PL-TADA;
+Adj+Gen+Strong:^Adj^Lea	PL-A;
+Adj+Voc:^Adj^Lea			PL-A;


LEXICON Adj2-2
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2A_sg;	
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

+Adj+Com+NotSlen:^Adj	PL-E;
+Adj+Com+Slender:^Adj	PL-E-SLENDER;
+Adj+Gen+Weak:^Adj		PL-TADA;
+Adj+Gen+Strong:^Adj	PL-E;
+Adj+Voc:^Adj			PL-E;

!!!!! REMOVING THESE VARIATIONS !!!!! 30/6/05
LEXICON Adj3-1
Base;
+Adj+Comp:0	#;
!!+Comp+Len:^Sé	#;
+Adj+Comp+Len:^Adj^Sé	#;
!!!+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
!!!+Adj:^Adj		Fem_gen-D4_sg;	
!!!+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

!!!+Adj+Masc+Com:^Adj		Masc_nom_sg;
!!!+Adj+Masc+Gen:^Adj		Masc_gen-D4_voc_sg;
!!!+Adj+Masc+Voc:^Adj		Masc_gen-D4_voc_sg;

!!!+Adj+Com+NotSlen:^Adj	PL-TADA;
!!!+Adj+Com+Slender:^Adj	PL-SLENDER;
!!!+Adj+Gen+Weak:^Adj		PL-TADA;
!!!+Adj+Gen+Strong:^Adj	PL-TADA;
!!!+Adj+Voc:^Adj			PL-TADA;


LEXICON Adj4-1
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj^Coim	Fem_gen-D2A_sg;		! sync + slen + e
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj^Coim	PL-A;			!sync+a
+Adj+Com+Slender:^Adj^Coim	PL-A-SLENDER;	!sé+sync+a
+Adj+Gen+Weak:^Adj			PL-TADA;
+Adj+Gen+Strong:^Adj^Coim	PL-A;			!sync+a
+Adj+Voc:^Adj^Coim			PL-A;			!sync+a

LEXICON Adj4-2
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj		Fem_gen-D2A_sg;		! slen + e
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj^Coim	PL-A;			!sync+a
+Adj+Com+Slender:^Adj^Coim	PL-A-SLENDER;	!sé+sync+a
+Adj+Gen+Weak:^Adj			PL-TADA;
+Adj+Gen+Strong:^Adj^Coim	PL-A;			!sync+a
+Adj+Voc:^Adj^Coim			PL-A;			!sync+a


LEXICON Adj4-3
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj^Coim	Fem_gen-D2A_sg;		! sync + slen + e
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj^Coim^Caol	PL-E;			!sync+slen +e
+Adj+Com+Slender:^Adj^Coim^Caol	PL-E-SLENDER;	!sé+sync+slen +e
+Adj+Gen+Weak:^Adj				PL-TADA;
+Adj+Gen+Strong:^Adj^Coim^Caol	PL-E;			!sync+slen +e
+Adj+Voc:^Adj^Coim^Caol			PL-E;			!sync+slen +e

LEXICON Adj4-4
Base;
+Adj+Fem+Com:^Adj		Fem_nom_voc_sg;
+Adj:^Adj^Coim	Fem_gen-D3_sg;		! sync + a
+Adj+Fem+Voc:^Adj		Fem_nom_voc_sg;

+Adj+Masc+Com:^Adj		Masc_nom_sg;
+Adj+Masc+Gen:^Adj		Masc_gen-D1_voc_sg;
+Adj+Masc+Voc:^Adj		Masc_gen-D1_voc_sg;

+Adj+Com+NotSlen:^Adj^Coim	PL-A;			!sync+a
+Adj+Com+Slender:^Adj^Coim	PL-A-SLENDER;	!sé+sync+a
+Adj+Gen+Weak:^Adj			PL-TADA;
+Adj+Gen+Strong:^Adj^Coim	PL-A;			!sync+a
+Adj+Voc:^Adj^Coim			PL-A;			!sync+a




!**************************************************************************
!ADJECTIVES QUALIFYING FEM SINGULAR NOUNS


LEXICON Fem_nom_voc_sg
! adj following a fem. noun is always lenited (^Sé) regardless of whether 
! the preceding noun is lenited or eclipsed (neither ??)
! as is the vocative after vocative particle "a"

+Sg:^Sé		#;

! SAME FORM IS USED FOR COMPARATIVE AND FEM GEN SG

LEXICON Fem_gen-D2A_sg			! like Noun Declension 2
+Fem+Gen+Sg:^Caole	#;
+Comp:^Caole	#;
!!+Comp+Len:^Caole^Sé	#;
+Comp+Len:^Caole^Sé	#;


LEXICON Fem_gen-D2B_sg			! like Noun Declension 2 but without "e" appended
+Fem+Gen+Sg:^Ath	#;
+Comp:^Ath	#;
!!+Comp+Len:^Ath^Sé	#;
+Comp+Len:^Ath^Sé	#;


LEXICON Fem_gen-D2C_sg			! a new change
+Fem+Gen+Sg:^Aththí	#;
+Comp:^Aththí	#;
!!+Comp+Len:^Aththí^Sé	#;
+Comp+Len:^Aththí^Sé	#;


LEXICON Fem_gen-D2D_sg			! a new change
+Fem+Gen+Sg:^Aththaí	#;
+Comp:^Aththaí	#;
!!+Comp+Len:^Aththaí^Sé	#;
+Comp+Len:^Aththaí^Sé	#;

LEXICON Fem_gen-D3_sg			! like Noun Declension 3
+Fem+Gen+Sg:^Leaa	#;
+Comp:^Leaa	#;
!!+Comp+Len:^Leaa^Sé	#;
+Comp+Len:^Leaa^Sé	#;

LEXICON Fem_gen-D4_sg			! like Noun Declension 4 (no change)
+Fem+Gen+Sg:0	#;
+Comp:0	#;
!!+Comp+Len:^Sé	#;
+Comp+Len:^Sé	#;



!ADJECTIVES QUALIFYING MASC SINGULARNOUNS

LEXICON Masc_nom_sg
! adj following a masc. noun is lenited only if the noun is lenited

! Len tags changed 27/5/2003 EUíD
!+Sg+LenYES:^Sé	#;				! ar an fhear "bheag" - on the small man
!+Sg+LenNO:0 	#;				! BUT ar an bhfear "beag" - on the small man

!!+Sg+Len:^Sé	#;				! Lenite if noun is lenited e.g. ar an fhear "bheag" - on the small man
+Sg:^Sé	#;				! Lenite if noun is lenited e.g. ar an fhear "bheag" - on the small man
+Sg:0 		#;				! Otherwise no lenition e.g. ar an bhfear "beag" - on the small man


LEXICON Masc_gen-D4_voc_sg		! like Noun Declension 4
	
+Sg:^Sé		#;				! hata an fhir bhig - the small man's hat
						! a fhir bhig - O small man

LEXICON Masc_gen-D1_voc_sg		! like Noun Declension 1

+Sg:^Caol^Sé	#;				! 



! ADJECTIVES QUALIFYING PLURAL NOUNS- COMMON TO BOTH GENDERS


LEXICON PL-A
+Pl:a		#;			! adj is NOT lenited after a noun ending in a 
					! broad consonantor a vowel! E.G. na mná móra - the big women

					! cathair (city) is a noun with strong plurals
					! soilse na cathracha móra - big cities' lights

					! vocative: a mhná móra - O big women
LEXICON PL-A-SLENDER

+Pl:a^Sé	#;			! adj IS lenited after a noun ending in a 
					! slender consonantE.G. na fir bheaga - the small men


LEXICON PL-E
+Pl:e		#;			! adj is NOT lenited after a noun ending in a 
					! broad consonantor a vowel

					! cathair (city) is a noun with strong plurals
					! obair na bpáistí maithe (work of the good childres)

					! vocative: a pháistí mhaithe - O good children
LEXICON PL-E-SLENDER

+Pl:e^Sé	#;			! adj IS lenited after a noun ending in a 
					! slender consonant

LEXICON PL-SLENDER

+Pl:^Sé		#;			! adj IS lenited after a noun ending in a 
					! slender consonant

LEXICON PL-TADA							
+Pl:0		#;			! bean (woman) is a noun with weak plurals
					! hataí na mban mór - the big womens' hats 
!------------------------------------------------------------------------------					

!************************************************************************** 
LEXICON AdjBASE
!!!!+Adj+Base:0		SéUrú;
Base;

! BASE FORM
LEXICON Base
+Adj+Base:0	#;
!!!!+Adj+Base:^IM^hv	#; 	! e.g. go hiontach
+Adj+Base+hPref:^IM^hv	#; 	! e.g. go hiontach
+Adj+Base+Len:^IM^Sé	#; 	! e.g. ba bhreá

LEXICON SéUrú
#;
+Len:^Adj^Sé	#;
!!!:^Adj^Sé	#;
+Ecl:^Adj^Urú	#;
!!!:^Adj^Urú	#;
